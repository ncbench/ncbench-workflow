from snakemake.utils import min_version
from snakemake.remote import SFTP
from snakemake.remote import HTTP


min_version("6.10.0")


configfile: "config/config.yaml"


envvars:
    "ZENODO_TOKEN",


include: "rules/common.smk"


https = HTTP.RemoteProvider()


module benchmark:
    snakefile:
        github("snakemake-workflows/dna-seq-benchmark", path="workflow/Snakefile", tag="v1.5.0")
    config:
        config


use rule * from benchmark as benchmark_*


rule download_zenodo:
    input:
        get_zenodo_input,
    output:
        "resources/zenodo/{section}/{entry}/{path}",
    shell:
        "mv {input} {output}"


rule download_https:
    input:
        https.remote("{url}"),
    output:
        "resources/https/{url}",
    shell:
        "mv {input} {output}"
