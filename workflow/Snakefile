from snakemake.utils import min_version
from snakemake.remote import SFTP


min_version("6.10.0")


configfile: "config/config.yaml"


envvars:
    "FTP_PASSWORD",
    "ZENODO_TOKEN",


include: "rules/common.smk"


ftp = SFTP.RemoteProvider(username="ngscnbench", password=os.environ["FTP_PASSWORD"])


module benchmark:
    snakefile: 
        github("snakemake-workflows/benchmark-giab", path="workflow/Snakefile", tag="v1.3.4")
    config:
        config


use rule * from benchmark as benchmark_*


rule download_ftp:
    input:
        ftp.remote("amset.ccg.uni-koeln.de/transfer/VCFs/{path}")
    output:
        "resources/ftp/{path}"
    shell:
        "cp {input} {output}"


rule download_zenodo:
    input:
        get_zenodo_input
    output:
        "resources/zenodo/{section}/{entry}/{path}"
    shell:
        "mv {input} {output}"